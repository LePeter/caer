IF (NOT STEREOCALIBRATION)
	SET(STEREOCALIBRATION 0 CACHE BOOL "Enable stereo camera calibration (using OpenCV)")
ENDIF()

IF (STEREOCALIBRATION)
	# Require new OpenCV 3.1 at least.
	PKG_CHECK_MODULES(OPENCV3 REQUIRED opencv>=3.1)

	SET(CALIB_INCDIRS ${CAER_INCDIRS} ${OPENCV3_INCLUDE_DIRS})
	SET(CALIB_LIBDIRS ${CAER_LIBDIRS} ${OPENCV3_LIBRARY_DIRS})

	SET(CALIB_CXX_LIBS ${CAER_CXX_LIBS} ${OPENCV3_LIBRARIES})

	# Audio beep support via Allegro library
	PKG_CHECK_MODULES(ALLEGRO5 allegro-5>=5.0.11)
	PKG_CHECK_MODULES(ALLEGRO5_MAIN allegro_main-5>=5.0.11)
	PKG_CHECK_MODULES(ALLEGRO5_AUDIO allegro_audio-5)
	PKG_CHECK_MODULES(ALLEGRO5_ACODEC allegro_acodec-5)

	IF (NOT ALLEGRO5_FOUND)
		# Check alternative pkg-config naming
		PKG_CHECK_MODULES(ALLEGRO5 allegro-5.0>=5.0.11)
		PKG_CHECK_MODULES(ALLEGRO5_MAIN allegro_main-5.0>=5.0.11)
		PKG_CHECK_MODULES(ALLEGRO5_AUDIO allegro_audio-5.0)
		PKG_CHECK_MODULES(ALLEGRO5_ACODEC allegro_acodec-5.0)
	ENDIF()

	IF (NOT ALLEGRO5_FOUND OR NOT ALLEGRO5_MAIN_FOUND)
		MESSAGE(FATAL_ERROR "Allegro-5 main library not found!")
		RETURN()
	ENDIF()

	IF (NOT ALLEGRO5_AUDIO_FOUND OR NOT ALLEGRO5_ACODEC_FOUND)
		MESSAGE(FATAL_ERROR "Allegro-5 audio and acodec addons not found!")
		RETURN()
	ENDIF()

	SET(CALIB_INCDIRS ${CALIB_INCDIRS} ${ALLEGRO5_AUDIO_INCLUDE_DIRS} ${ALLEGRO5_ACODEC_INCLUDE_DIRS})
	SET(CALIB_LIBDIRS ${CALIB_LIBDIRS} ${ALLEGRO5_AUDIO_LIBRARY_DIRS} ${ALLEGRO5_ACODEC_LIBRARY_DIRS})

	SET(CALIB_C_LIBS ${CAER_C_LIBS} ${ALLEGRO5_AUDIO_LIBRARIES} ${ALLEGRO5_ACODEC_LIBRARIES})

	INCLUDE_DIRECTORIES(${CALIB_INCDIRS})
	LINK_DIRECTORIES(${CALIB_LIBDIRS})

	ADD_LIBRARY(stereocalibration SHARED stereocalibration.c calibration.cpp calibration_wrapper.cpp)

	SET_TARGET_PROPERTIES(stereocalibration
		PROPERTIES
		PREFIX "caer_"
	)

	TARGET_LINK_LIBRARIES(stereocalibration ${CALIB_C_LIBS} ${CALIB_CXX_LIBS})

	INSTALL(TARGETS stereocalibration DESTINATION ${CM_SHARE_DIR})
ENDIF()
